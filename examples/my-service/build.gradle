subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url = 'http://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
}

project(':server') {
    dependencies {
        compile 'io.vertx:vertx-apex:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-core:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-service-factory:3.0.0-SNAPSHOT'
        compile project(':services:productService')
    }
}

project(':services:productService') {
    ext {
        generated = file("$projectDir/src/main/generated")
    }
    configurations {
        annotationProcessor
    }
    sourceSets {
        main {
            java {
                srcDirs generated
            }
        }
    }
    dependencies {
        compile 'io.vertx:vertx-auth-service:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-codegen:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-core:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-lang-js:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-lang-ruby:3.0.0-SNAPSHOT'
        compile 'io.vertx:vertx-service-proxy:3.0.0-SNAPSHOT'
        compile 'com.mchange:c3p0:0.9.5'
        compile 'com.mchange:mchange-commons-java:0.2.9'
        compile 'mysql:mysql-connector-java:5.1.35'
        compile 'org.jdbi:jdbi:2.62'

        annotationProcessor 'io.vertx:vertx-codegen:3.0.0-SNAPSHOT'
    }

    task cleanGenerated(type: Delete) {
        delete generated
    }

    compileJava {
        dependsOn cleanGenerated
        doFirst {
            mkdir generated
        }
        options.compilerArgs += [
                "-s", generated,
                "-AoutputDirectory=$projectDir/src/main",
                "-processor", "io.vertx.codegen.CodeGenProcessor"
        ]
        classpath += configurations.annotationProcessor
    }
}
